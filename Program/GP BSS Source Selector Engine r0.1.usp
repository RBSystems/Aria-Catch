/* GreenPoint TDI 

Development Notes _________________________________________________________________________ Development Notes /*
Dealer Name:	Crestron Electronics
System Name:
System Number:
Programmer:		PAC
Comments:
Development Notes _________________________________________________________________________ Development Notes */

// ________________________________________________________________________________ Module Compile Time Behavior
//#DEFINE_CONSTANT cDebug  1
//#SYMBOL_NAME "BSS Soundweb London Source Selector v4.0"
#CATEGORY                "34"
#Output_Shift            2
#DEFAULT_VOLATILE
#ENABLE_STACK_CHECKING

// ___________________________________________________________________________________________________ Constants

// _____________________________________________________________________________________________________ F1 Help
//#HINT                              ""
//#HELP_PDF_FILE ""

#HELP_BEGIN

#HELP_END
// ________________________________________________________________________________________ Simpl Input - Output 
Digital_Input _SKIP_                          ;
Digital_Input Subscribe                       ;
Buffer_Input  RX$[255]                        ;
String_Input  Object_ID[3]                    ;
Analog_Input  In                              ;

String_Output TX$                             ;
String_Output Object_ID_FB                    ;
Analog_Output In_FB                           ;

// _____________________________________________________________________________________________ Global Varibles
Integer g_iRx_OK               ;
String  g_sObject_ID[3]        ;
String  g_sReply[40]           ;

// __________________________________________________________________________________________________ Parameters
String_Parameter Default_Object_ID[3]                                                             ;
#BEGIN_PARAMETER_PROPERTIES Default_Object_ID
	propValidUnits       = unitString                                                             ;
	propDefaultUnit      = unitString                                                             ;
	propDefaultValue     = "\x00\x00\x00"                                                         ;
	propShortDescription = "3 Hex Characters retrieved from the .architect file"                  ;
#END_PARAMETER_PROPERTIES

// ___________________________________________________________________________________________________ Functions

// ________________________________________________________________________________________________ Simpl Events
Change In
{         
	MakeString(TX$, "\x88\x00\x00\x03%s\x00\x00\x00\x00\x00%s\x03\x03\x03\x03\x03", g_sObject_ID, Chr(In));
	If(Subscribe)
	{
		MakeString(TX$, "\x89\x00\x00\x03%s\x00\x00\x00\x00\x00\x00\x03\x03\x03\x03\x03", g_sObject_ID);	
	}
}
  
Push Subscribe
{	
	MakeString(TX$, "\x89\x00\x00\x03%s\x00\x00\x00\x00\x00\x00\x03\x03\x03\x03\x03", g_sObject_ID);
}

Release Subscribe
{
	MakeString(TX$, "\x8A\x00\x00\x03%s\x00\x00\x00\x00\x00\x00\x03\x03\x03\x03\x03", g_sObject_ID);
}

Change RX$
{
	If(g_iRx_OK)
	{	
		g_iRx_OK = 0;
		While(Find("\x03\x03\x03\x03\x03", RX$))
		{ 
			g_sReply = Remove("\x03\x03\x03\x03\x03", RX$)                                              ;
		
			While(Len(RX$) && Byte(RX$, 1) = 3 )
			{
				g_sReply = g_sReply + RemoveByLength(1, RX$)                                            ;
			}
			If((Mid(g_sReply, 6, 3) = "\x00\x00\x00") || (Mid(g_sReply, 6, 3) = g_sObject_ID))
			{
				In_FB = Byte(g_sReply, 14)  ;
			}
		}
		g_iRx_OK = 1;
	}	
}

Change Object_ID
{
	If(Len(Object_ID) = 3)
	{
		g_sObject_ID = Object_ID         ;
		Object_ID_FB = g_sObject_ID      ;
	}
}

// ________________________________________________________________________________________________ Start Up Code
Function Main()
{
	g_sObject_ID = Default_Object_ID  ;
	Object_ID_FB = g_sObject_ID       ;
	
	g_iRx_OK  = 1 ;
}

